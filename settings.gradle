pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
        maven { url "https://maven.aliyun.com/repository/google" }
        maven { url "https://maven.aliyun.com/repository/public" }
    }
    plugins {
        id("com.android.application") version "9.0.0"
        id("com.android.library") version "9.0.0"
        id("org.jetbrains.kotlin.android") version "2.3.10"
        id("dev.rikka.tools.autoresconfig") version "1.2.2"
        id("dev.rikka.tools.materialthemebuilder") version "1.5.1"
        id("dev.rikka.tools.refine") version "4.4.0"
    }
}
dependencyResolutionManagement {
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
        maven { url "https://maven.aliyun.com/repository/google" }
        maven { url "https://maven.aliyun.com/repository/public" }
    }
    versionCatalogs {
        libs {
            version('hidden-api', '4.4.0')
            library('hidden-compat', 'dev.rikka.hidden', 'compat').versionRef('hidden-api')
            library('hidden-stub', 'dev.rikka.hidden', 'stub').versionRef('hidden-api')

            version('refine', '4.4.0')
            library('refine-runtime', 'dev.rikka.tools.refine', 'runtime').versionRef('refine')
            library('refine-annotation', 'dev.rikka.tools.refine', 'annotation').versionRef('refine')
            library('refine-annotation-processor', 'dev.rikka.tools.refine', 'annotation-processor').versionRef('refine')
            plugin('refine', 'dev.rikka.tools.refine').versionRef('refine')
        }
    }
}

include ':module', ':ui'

/*
 * This file is part of Sui.
 *
 * Sui is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Sui is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Sui.  If not, see <https://www.gnu.org/licenses/>.
 *
 * Copyright (c) 2026 Sui Contributors
 */

import org.apache.tools.ant.DirectoryScanner

DirectoryScanner.removeDefaultExclude('**/.gitattributes')

def root = "api"

def propFile = file('local.properties')
def props = new Properties()

if (propFile.canRead()) {
    props.load(new FileInputStream(propFile))

    if (props != null) {
        if (props["api.useLocal"].equals("true")) {
            root = props["api.dir"]
        }
    }
}

include ':aidl'
project(':aidl').projectDir = file("$root${File.separator}aidl")

include ':rish'
project(':rish').projectDir = file("$root${File.separator}rish")

include ':shared'
project(':shared').projectDir = file("$root${File.separator}shared")

include ':api'
project(':api').projectDir = file("$root${File.separator}api")

include ':provider'
project(':provider').projectDir = file("$root${File.separator}provider")

include ':server-shared'
project(':server-shared').projectDir = file("$root${File.separator}server-shared")
