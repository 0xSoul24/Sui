import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens

import java.security.MessageDigest

plugins {
    id('com.android.application')
    id('dev.rikka.tools.refine.gradle-plugin')
}

apply from: file(rootProject.file('module.gradle'))

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode gitCommitCount
        versionName moduleVersion.substring(1)
        //ndkVersion rootProject.ext.ndkVersion
        externalNativeBuild {
            cmake {
                arguments "-DMODULE_NAME:STRING=$moduleLibraryName",
                        "-DRIRU_MODULE_API_VERSION=$moduleRiruApiVersion",
                        "-DRIRU_MODULE_VERSION=$moduleVersionCode",
                        "-DRIRU_MODULE_VERSION_NAME:STRING=\"$moduleVersion\"",
                        "-DANDROID_STL=none"

            }
        }
    }
    buildFeatures {
        viewBinding true
        prefab true
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }
    buildTypes {
        debug {
            multiDexEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dependenciesInfo {
        includeInApk false
    }
}

dependencies {
    implementation 'dev.rikka.tools.refine:runtime:3.0.3'

    implementation 'dev.rikka.ndk:riru:26.0.0'
    implementation 'dev.rikka.ndk.thirdparty:cxx:1.1.0'
    implementation 'dev.rikka.ndk.thirdparty:nativehelper:1.0.1'
    implementation 'dev.rikka.rikkax.parcelablelist:parcelablelist:1.0.0'

    implementation "androidx.annotation:annotation:1.2.0"

    implementation project(':aidl')
    implementation project(':shared')
    implementation project(':api')
    implementation project(':rish')
    implementation project(':server-shared')
    compileOnly project(':hidden-api-stub')
}

def outDir = file("$rootDir/out")

afterEvaluate {
    def isIDE = properties.containsKey('android.injected.invoked.from.ide')
    if (isIDE) {
        println("Invoked from IDE")
    } else {
        println("Invoked from command line")
    }

    android.applicationVariants.all { variant ->
        def variantCapped = variant.name.capitalize()
        def variantLowered = variant.name.toLowerCase()

        def zipName = "sui-${moduleVersion}-${variantLowered}.zip"
        def magiskDir = file("$outDir/magisk_module_${variantLowered}")

        tasks.getByName("pre${variantCapped}Build").dependsOn(":ui:assemble${variantCapped}")

        task("prepareMagiskFiles${variantCapped}", type: Sync) {
            dependsOn("assemble$variantCapped")

            def templatePath = "$rootDir/template/magisk_module"

            into magiskDir
            from(templatePath) {
                exclude 'riru.sh', 'module.prop'
            }
            from(templatePath) {
                include 'riru.sh'
                filter(ReplaceTokens.class, tokens: [
                        "RIRU_MODULE_LIB_NAME"             : moduleLibraryName,
                        "RIRU_MODULE_API_VERSION"          : moduleRiruApiVersion.toString(),
                        "RIRU_MODULE_MIN_API_VERSION"      : moduleMinRiruApiVersion.toString(),
                        "RIRU_MODULE_MIN_RIRU_VERSION_NAME": moduleMinRiruVersionName,
                ])
                filter(FixCrLfFilter.class,
                        eol: FixCrLfFilter.CrLf.newInstance("lf"))
            }
            from(templatePath) {
                include 'module.prop'
                expand([
                        id         : magiskModuleId,
                        name       : moduleName,
                        version    : moduleVersion,
                        versionCode: moduleVersionCode.toString(),
                        author     : moduleAuthor,
                        description: moduleDescription,
                ])
                filter(FixCrLfFilter.class,
                        eol: FixCrLfFilter.CrLf.newInstance("lf"))
            }
            from((variantLowered == "release") ?
                    "$buildDir/intermediates/dex/$variantLowered/minify${variantCapped}WithR8" :
                    "$buildDir/intermediates/dex/$variantLowered/mergeDex$variantCapped") {
                include 'classes.dex'
                rename { 'sui.dex' }
            }
            from("$buildDir/intermediates/stripped_native_libs/$variantLowered/out/lib") {
                into 'lib'
            }
            from("${rootProject.findProject('ui').buildDir}/outputs/apk/$variantLowered") {
                exclude 'output-metadata.json'
                rename { 'sui.apk' }
            }
            doLast {
                fileTree("$magiskDir").visit { f ->
                    if (f.directory) return
                    if (f.file.name == '.gitattributes') return

                    def md = MessageDigest.getInstance("SHA-256")
                    f.file.eachByte 4096, { bytes, size ->
                        md.update(bytes, 0, size)
                    }
                    file(f.file.path + ".sha256sum").text = md.digest().encodeHex()
                }
            }
        }

        task("zip${variantCapped}", type: Zip) {
            dependsOn("prepareMagiskFiles${variantCapped}")
            from magiskDir
            archiveName zipName
            destinationDir outDir
        }

        task("push${variantCapped}", type: Exec) {
            dependsOn("assemble${variantCapped}")
            workingDir outDir
            commandLine android.adbExecutable, "push", zipName, "/data/local/tmp/"
        }

        task("flash${variantCapped}", type: Exec) {
            dependsOn("push${variantCapped}")
            commandLine android.adbExecutable, "shell", "su", "-c",
                    "magisk --install-module /data/local/tmp/${zipName}"
        }

        task("flashAndReboot${variantCapped}", type: Exec) {
            dependsOn("flash${variantCapped}")
            commandLine android.adbExecutable, "shell", "reboot"
        }

        variant.assembleProvider.get().finalizedBy("zip${variantCapped}")
    }
}
