import org.apache.tools.ant.filters.FixCrLfFilter

import java.security.MessageDigest

plugins {
    id('com.android.application')
    id('dev.rikka.tools.refine')
}

apply from: file(rootProject.file('module.gradle'))

android {
    namespace "rikka.sui"
    defaultConfig {
        versionCode gitCommitCount
        versionName moduleVersion.substring(1)
        externalNativeBuild {
            cmake {
                arguments "-DZYGISK_MODULE_ID:STRING=\"$moduleId\"",
                        "-DANDROID_STL=none"
            }
        }
    }
    buildFeatures {
        viewBinding true
        prefab true
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.31.0+"
        }
    }
    buildTypes {
        debug {
            defaultConfig.minSdk 23
            multiDexEnabled false
        }
        release {
            minifyEnabled true
            shrinkResources false
            multiDexEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                debugSymbolLevel 'SYMBOL_TABLE'
            }
        }
    }
    lintOptions {
        checkReleaseBuilds false
    }
    dependenciesInfo {
        includeInApk false
    }
}

dependencies {
    implementation libs.refine.runtime

    implementation 'org.lsposed.libcxx:libcxx:29.0.14206865'
    implementation 'dev.rikka.ndk.thirdparty:nativehelper:1.0.1'
    implementation 'dev.rikka.rikkax.parcelablelist:parcelablelist:2.0.1'

    implementation "androidx.annotation:annotation:1.9.1"
    implementation 'androidx.core:core:1.17.0'

    implementation project(':aidl')
    implementation project(':shared')
    implementation project(':api')
    implementation project(':rish')
    implementation project(':server-shared')

    implementation libs.hidden.compat
    compileOnly libs.hidden.stub
}

def outDir = file("$rootDir/out")

androidComponents {
    onVariants(selector().all(), { variant ->
        def variantCapped = variant.name.capitalize()
        def variantLowered = variant.name.toLowerCase()
        def buildTypeCapped = variant.buildType.capitalize()
        def buildTypeLowered = variant.buildType.toLowerCase()

        def zipName = "sui-${moduleVersion}_${moduleVersionCode.toString()}-${buildTypeLowered}.zip"
        def magiskDir = file("$outDir/${buildTypeLowered}")

        tasks.register("prepareMagiskFiles${variantCapped}", Sync) {
            def templatePath = "$rootDir/template/magisk_module"

            into magiskDir
            from(templatePath) {
                exclude 'module.prop', 'action.sh'
            }
            from(templatePath) {
                include 'module.prop'
                expand([
                        id         : moduleId,
                        name       : moduleName,
                        version    : "${moduleVersion} (${moduleVersionCode.toString()})",
                        versionCode: moduleVersionCode.toString(),
                        author     : moduleAuthor,
                        description: moduleDescription,
                        updateJson : "https://xiaotong6666.github.io/Sui/sui_zygisk.json",
                ])
                filter(FixCrLfFilter.class,
                        eol: FixCrLfFilter.CrLf.newInstance("lf"))
            }
            from(templatePath) {
                include 'action.sh'
                filter(FixCrLfFilter.class,
                        eol: FixCrLfFilter.CrLf.newInstance("lf"))
            }
            from((buildTypeLowered == "release") ?
                    "$buildDir/intermediates/dex/${variantLowered}/minify${variantCapped}WithR8" :
                    "$buildDir/intermediates/dex/${variantLowered}/mergeDex$variantCapped") {
                include 'classes.dex'
                rename { 'sui.dex' }
            }
            from("$buildDir/intermediates/stripped_native_libs/${variantLowered}/strip${variantCapped}DebugSymbols/out/lib") {
                into 'lib'
            }
            from("${rootProject.findProject('ui').buildDir}/outputs/apk/$buildTypeLowered") {
                exclude 'output-metadata.json'
                rename { 'sui.apk' }
            }
            doLast {
                fileTree("$magiskDir").visit { f ->
                    if (f.directory) return
                    if (f.file.name == '.gitattributes') return

                    def md = MessageDigest.getInstance("SHA-256")
                    f.file.eachByte 4096, { bytes, size ->
                        md.update(bytes, 0, size)
                    }
                    file(f.file.path + ".sha256sum").text = md.digest().encodeHex()
                }
            }
        }

        tasks.register("zip${variantCapped}", Zip) {
            dependsOn("prepareMagiskFiles${variantCapped}")
            from magiskDir
            archiveFileName = zipName
            destinationDirectory = outDir
        }

        tasks.register("push${variantCapped}", Exec) {
            workingDir outDir
            commandLine "adb", "push", zipName, "/data/local/tmp/"
        }

        tasks.register("flash${variantCapped}", Exec) {
            dependsOn("push${variantCapped}")
            commandLine "adb", "shell", "su", "-c",
                    "magisk --install-module /data/local/tmp/${zipName}"
        }

        tasks.register("flashWithKsud${variantCapped}", Exec) {
            dependsOn("push${variantCapped}")
            commandLine "adb", "shell", "su", "-c",
                    "ksud module install /data/local/tmp/${zipName}"
        }

        tasks.register("flashAndReboot${variantCapped}", Exec) {
            dependsOn("flash${variantCapped}")
            commandLine "adb", "shell", "reboot"
            ignoreExitValue = true
        }

        tasks.register("flashWithKsudAndReboot${variantCapped}", Exec) {
            dependsOn("flashWithKsud${variantCapped}")
            commandLine "adb", "shell", "reboot"
            ignoreExitValue = true
        }
    })
}

afterEvaluate {
    ['Debug', 'Release'].each { buildType ->
        def buildTypeLowered = buildType.toLowerCase()

        tasks.named("pre${buildType}Build").configure {
            dependsOn(":ui:assemble${buildType}")
        }

        tasks.named("prepareMagiskFiles${buildType}").configure {
            dependsOn("assemble${buildType}")
        }

        tasks.named("assemble${buildType}").configure {
            finalizedBy("zip${buildType}")
        }

        tasks.named("push${buildType}").configure {
            dependsOn("assemble${buildType}")
        }
    }
}
